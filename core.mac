|*** 
	core.mac
	Core Macro
	exspes007
	
	"Someone once told me it taks a lot of skill and preperation to be effectively lazy."
	-- unknown
	
	GitHub: https://github.com/exspes007
	GitHub Code: https://github.com/exspes007/core
	GitHub Wiki: https://github.com/exspes007/core/wiki
	
	/invoke ${Macro.Undeclared}
	/echo ${EverQuest.LastMouseOver.Name}
	
	Required Plugins:	MQ2Cast, MQ2EQBC, EQBCS, MQ2Exchange, MQ2MoveUtils, MQ2Netbots, MQ2Debuffs
		
	Files:
	- core.mac									You are looking at it
	- core.ini									Script settings that affect all the toons and environment.
	- common.inc								All common use functions.
	- events.inc								Contains responses to EQ generated changes (environment/emotes).
	- hud.inc										Sets the hud.
	- declare.inc								99.9% of all variables, class INI creations, forced event, watched commands, alias, binds'.
	- test.inc									empty. testing purposes.
	- class.???.inc							One include for each class [WIZ|NEC|MAG|ENC|WAR|PAL|SHD|BST|BER|BRD|RNG|ROG|CLR|DRU|SHM|MNK]
	- misc stuff.txt						Just helpful info. old code. A forgotten attic of old stuff.
	- core.zone.ini							Zone spawn range, no cast, no melee, no mezz, NAMED, charm mobs
	- core.loot.ini							loot list. Leave, Keep, Destroy, Sell
	- blurb.inc									Expansion specific raid announcements. A lazy way to send raid instructions to all in RS.
	- mode.inc									All Non-Standard Mode controlls.
	- core.SERVER.CLS.NAME.ini	Character specific INI settings.
	- core.augmentdump.ini			list of all augments by toon.
	
	Naming Conventions:
	Local/Private Variables: _camelCasing						eg. _loopOutside, _count
	Outer/Global Variables: camelCasing							eg. Nuke1 or healIntervention1
	Running Timers: Timer_Use_For_Timer 						eg. timer_check_ammo
	Timer Times: tUse_ForTimer 											eg. tCheck_Ammo
	Functions: name_name_name() 										eg. check_ammo()
	Functions [class specific]: name_SHORTNAME()  	eg. disc_WAR()
	/goto :somethingmeaningful 											eg. :waitforrezbox
	DEFINE: ALLUPPERCASE 														eg. #define AMIDEAD
		
	Common Used Variables:
	coreAuto						bool			automatic or manual Mode
	Target2Kill					int				the ID of the mob that should be the focus of your wanton destruction
	isValidTarget				bool			is ths a valid Target
	timer_???						string		Prefix for running timers. [Timers] in the Toon INI(s) will hold most base set times.
	combat							string		What you are currently doing [MELEE|RANGE|NUKE|HEAL] DUAL is reserved for more then one mode and CC for Chanters/Bards.
	MobAgro							int				range that you will engage a mob at
	_count, _x, _b, _i	int				local loop counts
	_tmpID							int				the passed/local target ID
	tmpHealOverride			int				control hp heal reduction percentage. For zones that impose a HP/Mana cap. (no effect on stamina calculations)
	
	Common Used Functions:
	check_exit										returns {TRUE/FALSE] if there is a reason to not do the current sub
	create_timer									creates a timer. will declare the variable if needed
	core_cast											"SPELLNAME" [alt|disc|gem#|item|0] [MobID|0] [subname]
	writeINI											updates the related ini and the loaded variable
		
	Tab spaces set to 2 if you care.
	
	Credits/Inspiration
		- cast pre-condition added for nukes, dots and a few other things. inspired by raiddruid mac.
		- ultraedit v24 wordfile - http://www.macroquest2.com/phpBB3/viewtopic.php?f=46&t=19321&hilit=ultraedit (adapted and updated to current MQ2)
		- NinjaADVLoot - 	is the core of the do_loot loot function used with VERY heavy mods/butchering. http://www.macroquest2.com/phpBB3/viewtopic.php?f=49&t=19707
		- whoHas.mac [/whohas] - http://www.macroquest2.com/phpBB3/viewtopic.php?f=49&t=20212 (this is a genius idea)
		- puller/targ acquisition is very loosely based off nytemyst pull code for his SK and the healing foundation
		- i stole the base forage functions from someone years ago. Thank You Whomever. fish code based off same processes. and then promptly adapted. rewritten to use core.loot.ini list.

***|




#turbo 55
| #warning
#define DEBUG "/if (${switchDebugCore}) /echo \ar[\ax\aw${Time.Second}\ax : \aw${Macro.CurLine}\ax\ar]\ax"
#define DEBUGCAST "/if (${switchDebugCasting} || ${switchDebugCore}) /echo [\aw${Time.Second}\ax : \aw${Macro.CurLine}\ax]"
#define DEBUGCLASS "/if (${switchDebugClass} || ${switchDebugCore}) /echo \ar[\ax\aw${Time.Second}\ax : \aw${Macro.CurLine}\ax\ar]\ax"
#define DEBUGREZ "/if (${switchDebugRez} || ${switchDebugCore}) /echo \ar[\ax\aw${Time.Second}\ax : \aw${Macro.CurLine}\ax\ar]\ax"
#define DEBUGARRAY "/if (${switchDebugArray} || ${switchDebugCore}) /echo \ar[\ax\aw${Time.Second}\ax : \aw${Macro.CurLine}\ax\ar]\ax"
#define DEBUGMODE "/if (${switchDebugMode} || ${switchDebugCore}) /echo \ar[\ax\aw${Time.Second}\ax : \aw${Macro.CurLine}\ax\ar]\ax"
#define SDEBUG "/echo \ar[\ax\aw${Time.Second}\ax : \aw${Macro.CurLine}\ax\ar]\ax"
#define OUT "/echo ${c-}-\ax"
#define VOUT "/if (${verbose}) /echo ${c-}-\ax"
#define VMELEE "/if (${switchEchoMelee}) /echo ${c-}-\ax"
#define VSPELL "/if (${switchEchoSpell}) /echo ${c-}-\ax"
#define VITEM "/if (${switchEchoItem}) /echo ${c-}-\ax"
#define VDISC "/if (${switchEchoDisc}) /echo ${c-}-\ax"
#define VALT "/if (${switchEchoAlt}) /echo ${c-}-\ax"
#define VBC "/if (${switchEchoBC}) /call echos bc"
#define ECHOCHANNEL "/if (${Bool[${echoChannel}]}) /${echoChannel} "
#define HUDPATH ".\..\MQ2HUD.ini core"
#define LOG "/if (${Bool[${mqlog}]}) /mqlog ${Me.CleanName}: "
| #define AMIDEAD "/if ((${Me.State.Equal[HOVER]} || ${Me.Hovering} || ${Me.Dead}) && ${switchRezTake} || ${markerDead}) /call wait_for_rez"
| #define AMIDEAD "/if ((${Me.Dead}) && ${switchRezTake}) /call wait_for_rez"
#define AMIDEAD "/if (${watch_for_death[]}) /call watch_for_rezbox"
#define SIMPLEEXIT "/if (${Me.Moving} || ${Me.Stunned} || ${Stick.Active} || ${Me.Invis} || ${Me.State.Equal[FEIGN]} || ${switchAmIDead} || ${flagCallNeedRez}) /return"
#define CHECKEXIT "/if (${check_exit[]}) /return"
#define CHECKTIE "/if (${SetTie} && !${Bool[${timer_check_Tie}]}) /call set_tie"
#define CHECKREZ "/if (${coreAuto} && !${switchPull} && !${Me.Invis} && ${switchRezGive}) /call check_for_corpse"
#define CHECKSAFELIST "/if (!${safelist.Find[${_sender}]} && !${switchOverrideSafelist}) /return"
#define UPDATEHUD "/call update_HUD_notices"
#define CLEARCURSOR "/if ((${switchAutoCursor} && ${Cursor.ID} && !${timer_auto_cursor_pause}) || ${Bool[${autoInventoryItem.Find[${Cursor.Name}]}]}) /call sort_cursor"


#include tc\declare.inc
#include tc\common.inc
#include tc\MQ2Cast_Spell_Routines.inc
#include tc\mode.inc
#include tc\events.inc
#include tc\test.inc
#include tc\class.ber.inc
#include tc\class.brd.inc
#include tc\class.bst.inc
#include tc\class.clr.inc
#include tc\class.dru.inc
#include tc\class.enc.inc
#include tc\class.mag.inc
#include tc\class.mnk.inc
#include tc\class.nec.inc
#include tc\class.pal.inc
#include tc\class.rng.inc
#include tc\class.rog.inc
#include tc\class.shd.inc
#include tc\class.shm.inc
#include tc\class.war.inc
#include tc\class.wiz.inc
#include tc\hud.inc


|***
 *	DES: Main .. duhh..
 *	USE: 
 *	INI: bunches of 'em
 *  NOTE: 
 ***|
sub main()
	/mqclear
 	/declare INICore 											string 	outer tc\core.ini
 	/declare INIClass 										string 	outer tc\core.${MacroQuest.Server}.${Me.Class.ShortName}.${Me.DisplayName}.ini
	/declare INIZone		     							string 	outer tc\core.zone.ini
	/declare INILoot											string  outer	tc\core.loot.ini
	/declare INIWiki		     							string 	outer tc\core.wiki.ini
	/declare INIBlurb											string	outer tc\core.blurb.ini
	/declare INIAugDump     							string 	outer tc\core.augmentdump.ini
	/call set_declares
	/call declare_${Me.Class.ShortName}
	/call check_plugin
	/call intialize_item_buffs
	/call set_alias
	/call declare_timers
	/call echos startup 0 0 0 0
	/call verify_build
	/call Event_Zone
	/call base_preperation

	AMIDEAD

	LOG .. Starting Core:${coreBuild}.${MacroQuest.Server}.${Me.Class.ShortName}.${Me.DisplayName}
  /if (${switchOverrideLos}) VOUT ${ocaution} ${sep} Ignoring Line of Sight\ax

	VOUT Commands ${sep} ${cinfo}/info\ax
	OUT ${cgood}Ready\ax
  
	:loopmain
  	DEBUG ${cbug}:loopmain (Core: ${coreBuild})\ax
	  AMIDEAD
	  /doevents
  
		/if (!${timer_check_EQBC}) /call check_one_mind
		
		| what is this for??
		/varset combat ${If[${coreAuto},${Ini[${INIClass},Common,combat]},FALSE]}

		| sort /heal override ##
		/if (${tmpHealOverride} != ${HealOverride}) /varset tmpHealOverride ${If[${HealOverride},${Float[${HealOverride}].Centi},99.99]}
		| adjustHUD death count
		/if (!${timer_HUD_death_check}) /varset hudDeathCount ${Int[${Ini[${INICore},deathcount,${MacroQuest.Server}.${Me.Class.ShortName}.${Me.DisplayName}]}]}
		
		CHECKREZ	
	  UPDATEHUD FALSE	
	 	/doevents
		CLEARCURSOR

		| non class specific automated thingies.
		/if (${coreAuto}) {
			/if (${HomeSet}) /squelch /makecamp mpause
			
			| keep an eye out for real rezes
			/if (${flagCallNeedRez} && ${Window[ConfirmationDialogBox].Open}) /varset markerDead TRUE
			
			/call check1offBuffs
			AMIDEAD
			
			| check to see if someone needs a doctor?
			/if (${switchHealCure}) {
				/call cast_cure
			}
			/if (${autoRest} && !${switchPull} || ${autoRest} && ${switchPull} && !${SpawnCount[npc ${If[${switchOverrideLos},,los]} radius ${MobAgro} playerstate 4]}) /call do_rest
			/call check_buff_routines

			| fix exp
			/if (!${timer_check_EXP} && (!${SafeZone} || !${switchOverrideSafezone})) /call auto_adjust_EXP

			| fix ur pet!
			/if (!${timer_Check_Familiar}) /call sort_familiar
			
			| food/drink
			/if (${Bool[${clickFoodDispenser}]} && !${timer_click_food}) /call click_summon_food
			/if (${Bool[${clickDrinkDispenser}]} && !${timer_click_drink}) /call click_summon_drink

			| check constant AA buffs
			/if (${Bool[${AANow1}]} || ${Bool[${AANow2}]}) /call check_AA_now
			| check constant Disc buffs
			/if (${Bool[${DiscNow1}]} || ${Bool[${DiscNow2}]}) /call check_Disc_now

			| sort out return to camp
			/if (${HomeSet} && !${SpawnCount[npc radius ${combatRadius} playerstate 4 targetable]} && ((${Me.Y} != ${MakeCamp.AnchorY}) && (${Me.X} != ${MakeCamp.AnchorX})) && (!${SafeZone} || ${switchOverrideSafezone})) /makecamp return

		}
		
		/if (!${coreAuto} && ${Me.PctEndurance} < 32) {
			/if (${validate_class[FALSE, |WAR|PAL|SHD|ROG|BER|RNG|MON|BST]}) /call cast_disc_breather
		}

		| use those AA.. hoarding bastard
		/if (${switchAAFireworks} && !${timer_Waste_Fireworks}) /call cast_AA_fireworks

		| watch for spawn
		/if (${Bool[${watchSpawn}]} && !${timer_check_watch_spawn}) /call check_watch_spawn
		| guns need ammo
		/if (!${Bool[${FindItemCount[${RangeItemSummon}]}]} && ${Bool[${RangeItemSummon}]}) /call check_ammo
		/if (${sumFamiliarLeave} && ${Me.Pet.Name.Find[familiar]}) /pet get lost
		
		CHECKTIE

		| looter stuffs
		/if ((${coreAuto} || ${switchOverrideLoot}) && (!${SafeZone} || ${switchOverrideSafezone})) /call do_loot

		/doevents		
		
		| non normal modes of operation
		/call do_mode
	
		| use the one off clickies in the Toons INI
		/if (!${timer_check_odds_and_ends}) /call odds_and_ends


		| do we pull or assist	
		/if (!${Me.Invis} || (${coreAuto} && ${Select[${Me.Class.ShortName},ROG]} && ${switchSos})) {	
			/if (${coreAuto} && (!${SafeZone} || ${switchOverrideSafezone})) {
				/if (!${switchPull}) {
					/call get_assist_target	
				} else /if (${switchPull} && !${Target2Kill}) {	
					DEBUG get target <= ${MobAgro} = ${SpawnCount[npc ${If[${switchOverrideLos},,los]} radius ${MobAgro} targetable]}		
					/call get_target
				}
			}
		}
		/doevents
		/call core_${Me.Class.ShortName}
	/goto :loopmain
/return



|***
 *	DES: Settings I like to use. 
 *	USE: /call base_preperation
 *	INI: varries
 *  NOTE: You are free to adjust these. If you do not like, simply comment them out. it will not "hurt" the script.
 ***|
sub base_preperation()

	| reset some of the variables so the toons dont act all stupid.
	/if (!${Bool[${Raid.Members}]}) /call Event_noRaid
	
	| set me some xtargets
 	/if (${validate_expansion[TRUE, |16]} && !${Bool[${Group.Members}]} && !${Bool[${Raid.Members}]}) {
 		 /xtarget set 1 Autohater
 	} else /if (${Bool[${Group.Members}]} && !${Bool[${Raid.Members}]}) {
 		 /xtarget set 1 Groupassisttarget
 	}
 	
 	
	/if (${validate_expansion[TRUE, |16]} && ${Bool[${Raid.Members}]}) {
		/xtarget set 1 Raidassist1target
		/xtarget set 2 Autohater
	}
	
	| fuck these plugins in particular
	/if (${validate_plugin[FALSE, |MQ2Bucles]}) /plugin MQ2Bucles unload


	| disable smart assists for TLP w/o underfoot
	/if (!${validate_expansion[TRUE, |16]}) {
		/if (${useSmartAssistR}) /call writeIni useSmartAssistR FALSE
		/if (${useSmartAssistG}) /call writeIni useSmartAssistG FALSE
	}
	
	| clear the spawn watch
	/if (${Bool[${watchSpawn}]}) /call writeINI watchSpawn Common FALSE
	
	| a little display adjustments. If ur blind, get some glasses.
	/mqfont ${setMQFontSize}
	
	| clear rampage tank for healers
	/if (${Select[${Me.Class.ShortName},CLR,DRU,PAL,SHM]}) /call writeINI RampageTank Heal FALSE

	| why the fuck did Verant/SOE/DayBreak ever implement these as a default..??	
	/squelch /assist ${setAutoAssist}
	/squelch /tgb ${setTGB}
	
	| disable the melee plugin and reset combat so you just stand there.
	/if (${validate_plugin[FALSE, |mq2melee]}) {
		/melee off
		/squelch /melee reset
	}

	| bard stuffs
	/if (${validate_plugin[FALSE, |mq2twist]} && ${validate_class[FALSE, |BRD]}) {
		/squelch /twist clear
		/squelch /twist quiet
	}

	| cpu stuffs
	/if (${validate_plugin[FALSE, |mq2cpuload]}) /squelch /cpu auto

	| make sure netbots is running	
	/if (${validate_plugin[FALSE, |mq2netbots]}) /squelch /netbots ON send=ON grab=ON	

	| turn off forced burn routines if there still on
	| /if (${Bool[${burnForce}]}) /varset burnForce FALSE

	| turn off rez non guildies/group if it is still on. 'cause fuck everyone else.
	/if (${switchRezEveryone}) /call writeINI switchRezEveryone Heal FALSE

	| Show the HUD	
	/squelch /hud ${HudType}
	/squelch /loadhud ${HudName}
			
	| force sos switch for non rogues
	/if (${switchSos} && !${validate_class[FALSE, |ROG]}) /call writeINI switchSos Common FALSE
		
	| check debuffs		
	/call validate_debuff FALSE mezz
	/call validate_debuff FALSE malo
	/call validate_debuff FALSE tash
	/call validate_debuff FALSE cripple
	/call validate_debuff FALSE slow
	/call validate_debuff FALSE eradicate
	/call validate_debuff FALSE snare		
		
		
	| KillCode Warning for stupid fucks that like to AFK farm
	/if (${Select[${remoteKillKey},"CHANGE ME",NULL,FALSE]}) {
		VOUT ${sep} Hey.. You!
		VOUT ${owarning} ${sep} FIX: \aw/core killcode=\ax${remoteKillKey}
		VOUT ${cbad}CHANGE THIS\ax
	}		
		
/return